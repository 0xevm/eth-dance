FILE = ${ SOI ~ block ~ EOI }
statement = !{ forloop | assignment }
assignment = { (item ~ "=")? ~ expr }
forloop = {
  "for" ~ item ~ "in" ~ expr ~ "{"
    ~ block ~
  "}"
}
dot = { ":" | "." }

args = { arg ~ ("," ~ arg)* }
funccall = { ident? ~ dot ~ ident ~ "(" ~ args? ~ ")" }

ident = @{ ("$"|"@")? ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
int = @{ ASCII_DIGIT+ }
float = @{ int ~ "." ~ int }
number_suffix = @{
  "e" ~ int ~ "u"? |
  "f" ~ int? |
  "u"? ~ "q" ~ int? |
  "eth" | "gwei"
}
number = { (float | int) ~ number_suffix? }
fixed_array = { "[" ~ (expr ~ ("," ~ expr)* ~ ","?)? ~ "]"}

// https://github.com/pest-parser/book/blob/master/src/examples/rust/literals.md#strings
string = ${ ident? ~ "\"" ~ (raw_string | escape)* ~ "\"" }
raw_string = { (!("\\" | "\"") ~ ANY)+ }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte       = { "x" ~ ASCII_HEX_DIGIT{2} }
unicode    = { "u" ~ "{" ~ ASCII_HEX_DIGIT{1, 6} ~ "}" }
escape     = { "\\" ~ (predefined | byte | unicode) }

block = ${ (
  WHITESPACE* ~ COMMENT? ~ NEWLINE |
  WHITESPACE* ~ statement ~ (!NEWLINE ~ WHITESPACE)* ~ COMMENT? ~ NEWLINE
)* }

item = { ident ~ (":" ~ type)? }
arg = { expr ~ (":" ~ type)? }
expr = {
  funccall |
  string |
  number |
  fixed_array |
  ident
}
type = {
  string | ident |
  "[" ~ type ~ (";" ~ int)? ~ "]"
}

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }
