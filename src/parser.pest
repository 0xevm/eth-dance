FILE = ${ SOI ~ block ~ EOI }
statement = !{ forloop | assignment }
assignment = { (item ~ op_eq | (!"=" ~ op_eq))? ~ expr }
op_eq = { "=" | "<-" }
forloop = {
  "for" ~ item ~ "in" ~ expr ~ "{"
    ~ block ~
  "}"
}
dot = { "." }

item = { ident ~ (":" ~ type)? }

arg = { expr ~ (":" ~ type)? }
args = { arg ~ ("," ~ arg)* }
funccall = { expr_item? ~ dot ~ ident ~ "(" ~ args? ~ ")" }

ident_inner = _{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
ident = @{ "$"? ~ ident_inner | "@" ~ ident_inner? }
int = @{ ASCII_DIGIT+ }
float = @{ int ~ "." ~ int }
number_suffix = @{
  "e" ~ int ~ "u"? |
  "f" ~ int? |
  "u"? ~ "q" ~ int? |
  "eth" | "gwei"
}
number = ${ (float | int) ~ number_suffix? }
fixed_array = { "[" ~ (expr ~ ("," ~ expr)* ~ ","?)? ~ "]"}
index_access = { ident ~ "[" ~ (expr ~ ("," ~ expr)*) ~ "]" }

// https://github.com/pest-parser/book/blob/master/src/examples/rust/literals.md#strings
string = ${ ident? ~ "\"" ~ (raw_string | escape)* ~ "\"" }
raw_string = { (!("\\" | "\"") ~ ANY)+ }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte       = { "x" ~ ASCII_HEX_DIGIT{2} }
unicode    = { "u" ~ "{" ~ ASCII_HEX_DIGIT{1, 6} ~ "}" }
escape     = { "\\" ~ (predefined | byte | unicode) }

block = ${ (
  WHITESPACE* ~ COMMENT? ~ NEWLINE |
  WHITESPACE* ~ statement ~ (!NEWLINE ~ WHITESPACE)* ~ COMMENT? ~ NEWLINE
)* }

expr = { funccall | expr_item }
expr_item = _{
  string |
  number |
  fixed_array |
  index_access |
  ident
}
type = {
  string | ident |
  "[" ~ type ~ (";" ~ int)? ~ "]"
}

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "#" ~ (!NEWLINE ~ ANY)* }
