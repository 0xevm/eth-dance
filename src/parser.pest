FILE = { SOI ~ (statement? ~ NEWLINE)* ~ EOI }
statement = { (item ~ "=")? ~ expr }
dot = { ":" | "." }

args = { arg ~ ("," ~ arg)* }
funccall = { ident? ~ dot ~ ident ~ "(" ~ args? ~ ")" }

ident = @{ ("$"|"@")? ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
int = @{ ASCII_DIGIT+ }
float = @{ int ~ "." ~ int }
number_suffix = @{
  "e" ~ int ~ "u"? |
  "f" ~ int |
  "u"? ~ "q" ~ int? |
  "eth" | "gwei"
}
number = { (float | int) ~ number_suffix? }

// https://github.com/pest-parser/book/blob/master/src/examples/rust/literals.md#strings
string = ${ ident? ~ "\"" ~ (raw_string | escape)* ~ "\"" }
raw_string = { (!("\\" | "\"") ~ ANY)+ }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte       = { "x" ~ ASCII_HEX_DIGIT{2} }
unicode    = { "u" ~ "{" ~ ASCII_HEX_DIGIT{1, 6} ~ "}" }
escape     = { "\\" ~ (predefined | byte | unicode) }

item = { ident ~ (":" ~ type)? }
arg = { expr ~ (":" ~ type)? }
expr = {
  funccall |
  string |
  number |
  ident
}
type = @{ ident | string }

WHITESPACE = _{ " " }
COMMENT = { "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
