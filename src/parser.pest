FILE = { SOI ~ (statement? ~ NEWLINE)* ~ EOI }
statement = { (item ~ "=")? ~ expr }
dot = { ":" | "." }

args = { expr ~ ("," ~ expr)* }
funccall = { ident? ~ dot ~ ident ~ "(" ~ args? ~ ")" }

ident = @{ "$"? ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
item = { ident ~ (":" ~ ident)? }
int = @{ ASCII_DIGIT+ }
float = @{ int ~ "." ~ int }
number_suffix = @{ "u"? ~ ("f" | "q" | "d") ~ int? }
number = { (float | int) ~ number_suffix? }

// https://github.com/pest-parser/book/blob/master/src/examples/rust/literals.md#strings
string = ${ ident? ~ "\"" ~ (raw_string | escape)* ~ "\"" }
raw_string = { (!("\\" | "\"") ~ ANY)+ }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte       = { "x" ~ ASCII_HEX_DIGIT{2} }
unicode    = { "u" ~ "{" ~ ASCII_HEX_DIGIT{1, 6} ~ "}" }
escape     = { "\\" ~ (predefined | byte | unicode) }

expr = _{
  funccall |
  string |
  number |
  ident
}

WHITESPACE = _{ " " }
COMMENT = { "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }
